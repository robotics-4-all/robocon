import utils
import broker

Model:
(
    (imports*=Import)
    (robot=Robot)
    (broker=MessageBroker)
    (bridges*=Bridge)
)#
;

Robot:
    'Robot' '[' type=ROSType ']' name=ID '@' host=STRING
    '{'
    (
        ('TOPICS' topics*=ROSTopic)?
        ('SERVICES' services*=ROSService)?
        ('ACTIONS' actions*=ROSAction)?
        ('NODES' nodes*=ROSNode)?
    )#
    '}'
;

ROSType: "ROS2" | "ROS";

ROSTopic:
    name=ID '[' msg=STRING ',' uri=STRING ']'
;

ROSService:
    name=ID '[' msg=STRING ',' uri=STRING ']'
;

ROSAction:
    name=ID '[' msg=STRING ',' uri=STRING ']'
;

ROSNode:
    name=ID '{'
    (
        ('uri' ':' uri=STRING)?
        ('publishes' ':' '[' publishes*=[ROSTopic][','] ']')?
        ('subscribes' ':' '[' subscribes*=[ROSTopic][','] ']')?
        ('services' ':' '[' services*=[ROSService][','] ']')?
        ('actions' ':' '[' actions*=[ROSAction][','] ']')?
    )#
    '}'
;

Bridge: TopicBridge | ServiceBridge | ActionBridge | TFBridge | NodeBridge;

TopicBridge:
    'Bridge' '[' 'Topic' ']' name=ID lhs=TopicEndpoint '->' rhs=TopicEndpoint
    ('{'
        (when=WhenClause)?
        (unless=UnlessClause)?
        ('transform' ':' '{' transforms*=Transformation[','] '}')?
    '}')?
    ';'
;

Transformation:
    target=ID ':' expression=STRING
;

WhenClause:
    'when' ':' condition=STRING
;

UnlessClause:
    'unless' ':' condition=STRING
;


TopicEndpoint: ros=[ROSTopic] | broker=STRING;

ServiceBridge:
    'Bridge' '[' 'Service' ']' name=ID lhs=ServiceEndpoint '->' rhs=ServiceEndpoint ';'
;

ServiceEndpoint: ros=[ROSService] | broker=STRING;

ActionBridge:
    'Bridge' '[' 'Action' ']' name=ID lhs=ActionEndpoint '->' rhs=ActionEndpoint ';'
;

ActionEndpoint: ros=[ROSAction] | broker=STRING;

TFBridge:
    'Bridge' '[' 'TF' ']' name=ID (':' prefix=STRING)? ';'
;

NodeBridge:
    'Bridge' '[' 'Node' ']' name=ID node=[ROSNode] (':' prefix=STRING)?
    ('{'
        ('TOPICS' topic_maps*=NodeTopicMapping[','])?
        ('SERVICES' service_maps*=NodeServiceMapping[','])?
        ('ACTIONS' action_maps*=NodeActionMapping[','])?
    '}')?
    ';'
;

NodeTopicMapping:
    lhs=TopicEndpoint '->' rhs=TopicEndpoint
;

NodeServiceMapping:
    lhs=ServiceEndpoint '->' rhs=ServiceEndpoint
;

NodeActionMapping:
    lhs=ActionEndpoint '->' rhs=ActionEndpoint
;
// <--------------------------------------------------------------------

BridgeDirection: 'B2R' | 'R2B';